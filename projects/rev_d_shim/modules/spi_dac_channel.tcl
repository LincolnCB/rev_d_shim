### DAC Channel Module
## This module implements a DAC channel with an integrator and SPI interface.

# System signals
create_bd_pin -dir I -type clock spi_clk
create_bd_pin -dir I -type reset resetn

# Config parameters
create_bd_pin -dir I -from 31 -to 0 integ_window
create_bd_pin -dir I -from 14 -to 0 integ_thresh_avg
create_bd_pin -dir I integ_en

## Status signals
# System status
create_bd_pin -dir O setup_done
# Integrator status
create_bd_pin -dir O over_thresh
create_bd_pin -dir O thresh_overflow
create_bd_pin -dir O thresh_underflow
# DAC SPI controller status
create_bd_pin -dir O bad_cmd
create_bd_pin -dir O cal_oob
create_bd_pin -dir O dac_val_oob
create_bd_pin -dir O cmd_buf_underflow
create_bd_pin -dir O unexp_trig

# Commands
create_bd_pin -dir I -from 31 -to 0 dac_cmd
create_bd_pin -dir O dac_cmd_rd_en
create_bd_pin -dir I dac_cmd_empty

# Trigger
create_bd_pin -dir I trigger

# SPI interface signals
# Note that LDAC is an input because it's shared across multiple channels, generated by the SPI controller
create_bd_pin -dir O n_cs
create_bd_pin -dir O mosi
create_bd_pin -dir O ldac
create_bd_pin -dir I ldac_shared
create_bd_pin -dir I miso
create_bd_pin -dir I miso_sck

##################################################

### DAC SPI Controller

## DAC SPI core
cell lcb:user:ad5676_dac_ctrl:1.0 dac_spi {
  ABS_CAL_MAX 4096
} {
  clk spi_clk
  resetn resetn
  cmd_word_rd_en dac_cmd_rd_en
  cmd_word dac_cmd
  cmd_buf_empty dac_cmd_empty
  trigger trigger
  ldac_shared ldac_shared
  cmd_buf_underflow cmd_buf_underflow
  unexp_trig unexp_trig
  bad_cmd bad_cmd
  cal_oob cal_oob
  dac_val_oob dac_val_oob
  n_cs n_cs
  mosi mosi
  miso miso
  miso_sck miso_sck
  ldac ldac
}

##################################################

### Integrator


# Instantiate the threshold integrator module
cell lcb:user:threshold_integrator:1.0 threshold_core {} {
  clk spi_clk
  resetn resetn
  enable integ_en
  window integ_window
  threshold_average integ_thresh_avg
  sample_core_done dac_spi/setup_done
  abs_sample_concat dac_spi/abs_dac_val_concat
  err_overflow thresh_overflow
  err_underflow thresh_underflow
  over_thresh over_thresh
}

# Negate integ_en for setup checking
cell xilinx.com:ip:util_vector_logic n_integ_en {
  C_SIZE 1
  C_OPERATION not
} {
  Op1 integ_en
}

# Integrator marked done if integrator is done or disabled
cell xilinx.com:ip:util_vector_logic integ_done {
  C_SIZE 1
  C_OPERATION or
} {
  Op1 n_integ_en/Res
  Op2 threshold_core/setup_done
}

# Setup is done if integrator and DAC SPI controller are both done
cell xilinx.com:ip:util_vector_logic setup_done_and {
  C_SIZE 1
  C_OPERATION and
} {
  Op1 integ_done/Res
  Op2 dac_spi/setup_done
  Res setup_done
}
